class Keyboard {

    function char keyPressed()
    {
        return Memory.peek(24576);
    }

    function char readChar()
    {
        var char c, aux;

        do Output.printChar(0);

        while(true) {
            let aux = Keyboard.keyPressed();
            if (aux > 0) {
                let c = aux;
                while (true) {
                    let aux = Keyboard.keyPressed();
                    if (aux = 0) {
                        do Output.backSpace();
                        if (~(c = 129) & (~(c = 128))) {
                            do Output.printChar(c);
                        }
                        return c;
                    }
                }
            }
        }
        return 0;
    }

    function String readLine(String message)
    {
        var String str;
        var char c;

        do Output.printString(message); //prompt
        let str = String.new(80);

        while (true) {
            let c = Keyboard.readChar();
            if (c = 128) { //enter
                do Output.println();
                return str;
            }
            if (c = 129) { //backspace
                do str.eraseLastChar();
                do Output.backSpace();
            }
            else {
                let str = str.appendChar(c);
            }
        }

        return 0;
    }
    
    function int readInt(String message)
    {
        var String str;
        let str = Keyboard.readLine(message);
        return str.intValue();
    }
}